# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=consolekit
pkgver=0.4.6
pkgrel=4
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('polkit' 'zlib' 'libx11' 'dbus-glib')
makedepends=('pkgconfig' 'xmlto' 'docbook-xsl')
options=(!libtool)
#source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-$pkgver.tar.bz2
# this has not been released yet, so this is a snapshot
source=(ftp://ftp.archlinux.org/other/${pkgname}/ConsoleKit-${pkgver}.tar.bz2
        pam-foreground-compat.ck consolekit.logrotate consolekit.tmpfiles.conf)
md5sums=('810990b607e338f06a6396c92b8a4a06'
         'a8a4de71d9b0549b8143e5f6c2a36fc7'
         '6fefa451d9fe2fc6d6269629d3529793'
         '8c5a8059db515d40fae1379daf084094')

build() {
  cd "$srcdir/ConsoleKit-$pkgver"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-pam-module \
      --with-pam-module-dir=/usr/lib/security \
      --enable-docbook-docs \
      --enable-udev-acl
  make
}

package() {
  cd "$srcdir/ConsoleKit-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 "$srcdir/pam-foreground-compat.ck" "$pkgdir/usr/lib/ConsoleKit/run-session.d/"

  # install the logrotate config
  install -D -m644 "$srcdir/consolekit.logrotate" "$pkgdir/etc/logrotate.d/consolekit"

  install -D -m644 "$srcdir/consolekit.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/consolekit.conf"

  rm -rf "${pkgdir}/var/run"
}
