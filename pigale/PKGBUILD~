# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Gaetan Bisson <gaetan.bisson@loria.fr>

pkgname=pigale
pkgver=1.3.14
pkgrel=1
pkgdesc="A graph editor and a C++ algorithm library essentially concerned with planar graphs"
arch=('i686' 'x86_64')
url="http://pigale.sourceforge.net/"
license=('GPL2')
depends=('qt' 'freeglut')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop"
	"freeglut-lib.patch"
        "installation-paths.patch"
	"qt.pro" )
md5sums=('5ef62c616208aa016269e93eea5bfa5f'
         '8b803baa95e387c05f63fd104c8720c9'
         '4d9ec88d5ea52596dde1abcebd80b55f'
	 'e4da617d6adbaf74de88716a794b0e2a '
         'b4973c1554d33de6d1a19bf6fbfa9093'
	 'e630556ba7bbc496835324969e78df8a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  aclocal && automake -ac && autoconf
  ./configure \
    --prefix=/usr/share/${pkgname} \
    --with-qt4=/usr/share/qt \
    QT4=/usr

  # Apply patches
  patch -Np1 -i ../installation-paths.patch
  patch -Np1 -i ../freeglut-lib.patch
  cp "$srcdir/qt.pro" "$srcdir/${pkgname}-${pkgver}/qt/qt.pro"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  # Install data files
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  # Install binaries and libraries
  mv "${pkgdir}/usr/bin/"{client,pigale-client}
  install -Dm755 qt/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 lib/libtgraph.a "${pkgdir}/usr/lib/libtgraph.a"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
