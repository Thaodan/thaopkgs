# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=kdeplasma-applets-menubar
pkgver=0.1.18
pkgrel=1
pkgdesc="A Plasma widget to display menubar of application windows"
arch=('i686' 'x86_64')
url='https://launchpad.net/plasma-widget-menubar'
license=('GPL')
depends=('kdelibs' 'qjson')
makedepends=('cmake' 'automoc4')
conflicts=('plasma-widget-menubar')
replaces=('plasma-widget-menubar')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps'
            'libreoffice-extension-menubar: support for LibreOffice')
source=("https://launchpad.net/plasma-widget-menubar/trunk/$pkgver/+download/plasma-widget-menubar-$pkgver.tar.bz2")
md5sums=('37055ea768d561e8ba7acc1e2ce3502e')


build() {
  mkdir build
  cd build
  cmake ../plasma-widget-menubar-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
