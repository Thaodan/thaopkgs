# Maintainer: tuxce <tuxce.net@gmail.com>
pkgname=package-query-git
pkgver=1.1.1.g1cc5084
pkgrel=1
pkgdesc="Query ALPM and AUR"
arch=('i686' 'x86_64')
url="http://gitweb.archlinux.fr/package-query.git/"
license=('GPL')
depends=('pacman>=4.1' curl 'yajl>=2.0')
makedepends=('git')
conflicts=('package-query')
provides=('package-query=1.0')
source=('git+http://projects.archlinux.fr/package-query.git')
md5sums=('SKIP')
_gitname=package-query

pkgver () {
  cd $_gitname
  echo $(git describe --always | sed 's/-/./g')
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure --localstatedir=/var --prefix=/usr \
              --sysconfdir=/etc --with-aur-url=https://aur.archlinux.org
  make
}

package () {
  cd $_gitname
  make DESTDIR=$pkgdir install
}

# vim:set ts=4 sw=4 et:
