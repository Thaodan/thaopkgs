# $Id: PKGBUILD 79181 2012-10-31 07:25:46Z tpowa $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module-pf
_pkgname=vhba-module
pkgver=20120422
_extramodules=extramodules-3.6-pf
pkgrel=14
pkgdesc="Kernel module that emulates SCSI devices, for linux-pf"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux-pf>=3.6' 'linux-pf<3.7')
provides=( "vbha-module=$pkgver" )
makedepends=('linux-pf-headers')
options=(!makeflags)
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('d97372da1d270d1605742b2995fb6678'
         'b5e82d0160e7a181219b67c1794d5c27')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"

  make KDIR=/usr/lib/modules/$_kernver/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  install -Dm644 vhba.ko \
    "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"

  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" \
    "$startdir/$pkgname.install"

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"
}

# vim:set ts=2 sw=2 et:
