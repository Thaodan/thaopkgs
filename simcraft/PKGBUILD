# Maintainer: Bjoern Bidar <theodorstormgrade@gmail.com>
_gui=true
pkgname=simcraft 
_pkgname=simc
_simver=520
_simrel=6
pkgver=${_simver}_${_simrel}
pkgrel=2
pkgdesc="SimulationCraft is a tool to explore combat mechanics in the popular MMO RPG World of Warcraft (tm)"
url="http://code.google.com/p/simulationcraft"
arch=('i686' 'x86_64')
license=('GPL3')
depends=( 'openssl' )
if [ $_gui = true ] ; then
  depends+=( 'qt5-base' 'qt5-webkit')
fi
install=$pkgname.install
source=("http://simulationcraft.googlecode.com/files/$_pkgname-${_simver}-${_simrel}.zip" 
  'SimulationCraft.desktop'
  'detect_qt5_better.fix.patch'
  'dont_build_engine_again.fix.patch'
)
md5sums=('076a48b70fa576dcc219a3c22bb1be2e'
         '527f78bab42ec0a524388da12e3ce9e0'
         '6b22c9ab21438124c589c05a541bba19'
         '6ac73864b1e725176e3c3126f6ba248f')

prepare() { 
  if [ $_gui = true ] ; then
    cd $srcdir
    qmake  INSTALL_PREFIX=bin PREFIX=/usr  simcqt.pro -o Makefile   
    patch -Np1 < detect_qt5_better.fix.patch
    patch -Np1 < dont_build_engine_again.fix.patch
  fi
}

build() {
  cd $srcdir/engine
  make PREFIX=/usr  CFLAGS+="$CFLAGS"  CXXFLAGS+="$CXXFLAGS" LDFLAGS+="$LDFLAGS"
  if [ $_gui = true ] ; then
    cd ..
    make PREFIX=/usr  
  fi 
}

package()
{
  cd "$srcdir"
  for profile in $( find profiles -type f); do
    install -Dm644 $profile   "$pkgdir/usr/share/simcraft/$profile"
  done
  install -Dm755 engine/simc     "$pkgdir/usr/bin/simc"

  if [ $_gui = true ] ; then
    install -Dm644 debian/SimulationCraft.xpm "$pkgdir/usr/share/pixmaps/SimulationCraft.xpm"
    install -Dm644 "$srcdir/SimulationCraft.desktop" "$pkgdir/usr/share/applications/SimulationCraft.desktop"
    for _locale in locale/* ; do
      install -Dm644 $_locale  $pkgdir/usr/share/simcraft/$_locale
    done
    install -Dm755 SimulationCraft $pkgdir/usr/bin/simcraft
  fi

  for doc in Welcome.html Welcome.png Legend.html READ_ME_FIRST.txt ; do
    install -Dm644 $doc $pkgdir/usr/share/doc/simcraft/$doc
  done

    

}
