## Maintainer: Bjoern Bidar <theodorstormgrade@gmail.com>
_gui=true
_svn=false
pkgname=simcraft 
_pkgname=simc
_simver=520
_simrel=10
pkgver=${_simver}_${_simrel}
pkgrel=2
pkgdesc="SimulationCraft is a tool to explore combat mechanics in the popular MMO RPG World of Warcraft (tm)"
url="http://code.google.com/p/simulationcraft"
arch=('i686' 'x86_64')
license=('GPL3')
depends=( 'openssl' )
if [ $_gui = true ] ; then
  depends+=( 'qt5-base' 'qt5-webkit')
fi
install=$pkgname.install
source=("http://simulationcraft.googlecode.com/files/$_pkgname-${_simver}-${_simrel}-src.zip" 
  'SimulationCraft.desktop'
  'r16463_fix_temp_dir.patch'
  'dont_build_engine_again.fix.patch'
)
if [ $_svn = true  ] ; then
   makepends='svn'
   _svnurl=http://simulationcraft.googlecode.com/svn/trunk 
fi

md5sums=('b7a903fe8e4445daa31835aecbcea294'
         '527f78bab42ec0a524388da12e3ce9e0'
         'f4a495ba0ca2bebc51860f1b1af6611f'
         '3a670f9abc22a38edb50a25bec6505f8')

prepare() { 
  if [ $_svn = true ] ; then
     svn checkout $_svnurl ${_pkgname}-${_simver}-${_simrel}-source
  fi
  cd $srcdir
  dos2unix ${_pkgname}-${_simver}-${_simrel}-source/engine/sc_main.cpp
  patch -Np2 < $srcdir/r16463_fix_temp_dir.patch
  if [ $_gui = true ] ; then
    cd ${_pkgname}-${_simver}-${_simrel}-source
    #tr -d '\r' < simcqt.pro > simcqt.pro
#    dos2unix simcqt.pro
    patch -N < $srcdir/dont_build_engine_again.fix.patch
    qmake  INSTALL_PREFIX=bin PREFIX=/usr CONFIG+='to_install' simcqt.pro -o Makefile   
  fi

}

build() {
  cd $srcdir/$_pkgname-${_simver}-${_simrel}-source/engine   
  make PREFIX=/usr  CFLAGS+="$CFLAGS"  CXXFLAGS+="$CXXFLAGS" LDFLAGS+="$LDFLAGS"
  if [ $_gui = true ] ; then
    cd ..
    make PREFIX=/usr  
  fi 
}

package()
{
  cd $srcdir/$_pkgname-${_simver}-${_simrel}-source
  for profile in $( find profiles -type f); do
    install -Dm644 $profile   "$pkgdir/usr/share/simcraft/$profile"
  done
  install -Dm755 engine/simc     "$pkgdir/usr/bin/simc"

  if [ $_gui = true ] ; then
    install -Dm644 debian/SimulationCraft.xpm "$pkgdir/usr/share/pixmaps/SimulationCraft.xpm"
    install -Dm644 "$srcdir/SimulationCraft.desktop" "$pkgdir/usr/share/applications/SimulationCraft.desktop"
    for _locale in locale/* ; do
      install -Dm644 $_locale  $pkgdir/usr/share/simcraft/$_locale
    done
    install -Dm755 SimulationCraft $pkgdir/usr/bin/simcraft
  fi

  for doc in Welcome.html Welcome.png Legend.html READ_ME_FIRST.txt ; do
    install -Dm644 $doc $pkgdir/usr/share/doc/simcraft/$doc
  done

    

}
