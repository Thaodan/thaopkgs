## Maintainer: Bjoern Bidar <theodorstormgrade@gmail.com>
_gui=true
pkgname=simcraft 
_pkgname=simc
_simver=520
_simrel=10
pkgver=${_simver}_${_simrel}
pkgrel=1
pkgdesc="SimulationCraft is a tool to explore combat mechanics in the popular MMO RPG World of Warcraft (tm)"
url="http://code.google.com/p/simulationcraft"
arch=('i686' 'x86_64')
license=('GPL3')
depends=( 'openssl' )
if [ $_gui = true ] ; then
  depends+=( 'qt5-base' 'qt5-webkit')
fi
makedepends='dos2unix'
install=$pkgname.install
source=("http://simulationcraft.googlecode.com/files/$_pkgname-${_simver}-${_simrel}-src.zip" 
  'SimulationCraft.desktop'
  'dont_build_engine_again.fix.patch'
)
md5sums=('b7a903fe8e4445daa31835aecbcea294'
         '527f78bab42ec0a524388da12e3ce9e0'
         '3e3e02f49090f2b93137c2a2beb2d53d')

prepare() { 
  if [ $_gui = true ] ; then
    cd $srcdir/$_pkgname-${_simver}-${_simrel}-source 
    #tr -d '\r' < simcqt.pro > simcqt.pro
    dos2unix simcqt.pro
    patch -N < $srcdir/dont_build_engine_again.fix.patch
    qmake  INSTALL_PREFIX=bin PREFIX=/usr CONFIG+='to_install' simcqt.pro -o Makefile   
  fi
}

build() {
  cd $srcdir/$_pkgname-${_simver}-${_simrel}-source/engine   
  make PREFIX=/usr  CFLAGS+="$CFLAGS"  CXXFLAGS+="$CXXFLAGS" LDFLAGS+="$LDFLAGS"
  if [ $_gui = true ] ; then
    cd ..
    make PREFIX=/usr  
  fi 
}

package()
{
  cd $srcdir/$_pkgname-${_simver}-${_simrel}-source
  for profile in $( find profiles -type f); do
    install -Dm644 $profile   "$pkgdir/usr/share/simcraft/$profile"
  done
  install -Dm755 engine/simc     "$pkgdir/usr/bin/simc"

  if [ $_gui = true ] ; then
    install -Dm644 debian/SimulationCraft.xpm "$pkgdir/usr/share/pixmaps/SimulationCraft.xpm"
    install -Dm644 "$srcdir/SimulationCraft.desktop" "$pkgdir/usr/share/applications/SimulationCraft.desktop"
    for _locale in locale/* ; do
      install -Dm644 $_locale  $pkgdir/usr/share/simcraft/$_locale
    done
    install -Dm755 SimulationCraft $pkgdir/usr/bin/simcraft
  fi

  for doc in Welcome.html Welcome.png Legend.html READ_ME_FIRST.txt ; do
    install -Dm644 $doc $pkgdir/usr/share/doc/simcraft/$doc
  done

    

}
